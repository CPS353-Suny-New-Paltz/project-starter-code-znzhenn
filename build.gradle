plugins {
    id 'java'
    id 'checkstyle'
    id 'eclipse'
    id 'com.google.protobuf' version '0.9.4'
}

eclipse.project.name = 'sweProjectJenn' 

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

def grpcVersion = '1.59.1'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion


sourceSets {
	main {
		proto{
			srcDirs =['protobuf']
		}
	  java {
		srcDirs = ['src',
					'build/generated/source/proto/main/grpc',
					'build/generated/source/proto/main/java']
	  }
	}
	test {
	  java {
	     srcDirs = ['test']
	  }
	 }
}

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn tasks.withType(com.google.protobuf.gradle.GenerateProtoTask)
}


dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.10.2')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.10.2')
    testImplementation('org.mockito:mockito-core:5.10.0')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
    implementation('com.google.code.gson:gson:2.13.1')
    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.0'
    
    // grpc dependencies
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "io.grpc:grpc-services:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
	
}

test {
    useJUnitPlatform()
    	ignoreFailures = false
}

checkstyle {
	toolVersion = '10.12.0'  // pin to a stable Checkstyle version
    configFile = file('checkstyle_checks.xml')
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

